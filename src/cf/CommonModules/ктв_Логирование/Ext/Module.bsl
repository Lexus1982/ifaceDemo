////////////////////////////////////////////////////////////////////////////////
// Подсистема "Логирование". (Интерфейсный модуль логирования)
//
////////////////////////////////////////////////////////////////////////////////


#Область ИнтерфейсныеПроцедуры

Функция ПолучитьЛог(ИмяЛога, ПараметрыЗаписи = Неопределено) Экспорт
	
	Лог = Неопределено;
	
	#Если Сервер ИЛИ ВнешнееСоединение Тогда
		Лог = Обработки.ктв_Логирование.Создать();
	#Иначе
		Лог = ПолучитьФорму("Обработка.ктв_Логирование.Форма");
	#КонецЕсли
	
	Лог.ИмяЛога(ИмяЛога);
	Лог.ПараметрыЗаписи(ПараметрыЗаписи);
	
	Возврат Лог;
	
КонецФункции //ПолучитьЛог()

Функция УровниЛога() Экспорт
	
	УровниЛога = Новый Структура;
	УровниЛога.Вставить("Отладка"        , 0);
	УровниЛога.Вставить("Информация"     , 1);
	УровниЛога.Вставить("Предупреждение" , 2);
	УровниЛога.Вставить("Ошибка"         , 3);
	УровниЛога.Вставить("КритичнаяОшибка", 4);
	УровниЛога.Вставить("Отключить"      , 5);
		
	Возврат Новый ФиксированнаяСтруктура(УровниЛога);

КонецФункции //УровниЛога()

// Процедура выполняет запись в лог по всем способам логирования
//
// Параметры:
//	ИмяЛога					- Строка					- имя лога
//	Текст					- Строка					- текст выводимого сообщения
//	УровеньЛога				- Число						- уровень лога: Отладка, Информация, ошибка и т.п.
//	ПараметрыЗаписи			- Структура					- дополнительные параметры записи информации в лог
//
Процедура ЗаписатьВЛог(ИмяЛога, Текст, УровеньЛога = 1, ПараметрыЗаписи = Неопределено) Экспорт
	
	Если НЕ ТипЗнч(ПараметрыЗаписи) = Тип("Структура") Тогда
		ПараметрыЗаписи = Новый Структура();
	КонецЕсли;
	
	ПараметрыЗаписи.Вставить("ИмяЛога"		, ИмяЛога);
	ПараметрыЗаписи.Вставить("УровеньЛога"	, УровеньЛога);
	
	СпособыЛогирования = ктв_ЛогированиеВызовСервера.ПолучитьСпособыЛогирования(ИмяЛога);
	
	Для Каждого ТекСпособ Из СпособыЛогирования Цикл
		#Если НЕ (Сервер ИЛИ ВнешнееСоединение) Тогда
			ктв_ЛогированиеКлиент.ЗаписатьВЛог(ТекСпособ, ИмяЛога, Текст, ПараметрыЗаписи);
		#КонецЕсли
		ктв_ЛогированиеВызовСервера.ЗаписатьВЛог(ТекСпособ, ИмяЛога, Текст, ПараметрыЗаписи);
	КонецЦикла;
	
КонецПроцедуры //ЗаписатьВЛог()

#КонецОбласти 

